# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: xaw <xawl.ch@gmail.com>
pkgname=arcolinux-cwm
_destname1="/etc/X11/xinit/xinitrc.d/"
_licensedir="/usr/share/arcolinux/licenses/"
pkgver=6.7
pkgrel=19
pkgdesc="OpenBSD fork of calmwm, a clean and lightweight window manager - changed for ArcoLinux"
arch=('i686' 'x86_64')
url="http://www.openbsd.org/cgi-bin/man.cgi?query=cwm&sektion=1"
license=('BSD')
depends=('libxft' 'libxrandr')
changelog=cwm.changelog
conflicts=('cwm')
makedepends=('git')
source=("$pkgname::git+https://github.com/chneukirchen/cwm.git#tag=v${pkgver}" 'cwm.desktop' )
sha256sums=('SKIP'
            '806263f369b882f8655cc49920c911a1bde8d20d592afb085c58abf9a12e75a4')

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm755 "${srcdir}/cwm.desktop" "${pkgdir}/usr/share/xsessions/cwm.desktop"
  install -Dm755 <(sed -n '/Copyright/,/PERFORMANCE/p' <calmwm.c) "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
